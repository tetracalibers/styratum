SyrmCascade {
  /* override */
  space := whitespace | lineTerminator
  
  /* root */
  Cascade = "<Cascade>" space* Block* space* "</Cascade>"
  
  /* -------------------------------------------------------------------------- */
  /* block                                                                      */
  /* -------------------------------------------------------------------------- */
  
  Block = 
    | DeclarationsBlock -- basic
    | DeclarationsBlockWithPrefix -- prefixed
    | DeclarationsBlockWithPrefix syrmSuffixAnnotation -- surrounded
  DeclarationsBlockWithPrefix = syrmPrefixAnnotation DeclarationsBlock
  DeclarationsBlock = SelectorList "{" DeclarationList "}"
  
  /* -------------------------------------------------------------------------- */
  /* syrm support                                                               */
  /* -------------------------------------------------------------------------- */

  syrmPrefixAnnotation = truthyKeyword | falsyKeyword | existKeyword
  syrmSuffixAnnotation = invertKeyword
  
  propsFunc = "props" "(" jsIdentifier ")"
  truthyKeyword = "@" "truthy" "(" jsIdentifier ")"
  falsyKeyword = "@" "falsy" "(" jsIdentifier")"
  existKeyword = "@" "exist" "(" jsIdentifier ")"
  elseKeyword = "@" "else"
  invertKeyword = "@" "invert"
  collectionKeyword = "@" "collection"
  
  /* -------------------------------------------------------------------------- */
  /* declaration                                                                */
  /* -------------------------------------------------------------------------- */
  
  DeclarationList = declaration+
  declaration = propertyName space* ":" space* propertyValue ";"
  
  /* -------------------------------------------------------------------------- */
  /* property name                                                              */
  /* -------------------------------------------------------------------------- */
  
  propertyName = kebabCase | collectionKeyword
  
  /* -------------------------------------------------------------------------- */
  /* property Value                                                             */
  /* -------------------------------------------------------------------------- */
  
  propertyValue = (propsFunc | propertyValueFunc | kebabCase | pascalCase | formula)+
  propertyValueFunc = kebabCase "(" formula ("," formula)* ")"
  
  /* -------------------------------------------------------------------------- */
  /* selector                                                                   */
  /* -------------------------------------------------------------------------- */
  
  /* selector list */
  SelectorList = selector ("," selector)*
  
  /* selector */
  selector = basicSelector | compositeSelector | pseudoSelector
  
  /* pseudo selector */
  pseudoSelector = 
    | basicSelector pseudo -- basic
    | pseudo -- abbreviated
  
  /* pseudo */
  pseudo = pseudoElement | pseudoClass | pseudoFunc
  pseudoFunc = (pseudoElement pseudoArg) | (pseudoClass pseudoArg)
  pseudoElement = "::" kebabCase
  pseudoClass = ":" kebabCase
  pseudoArg = "(" (selector | letter+ | digit+ | nth | propsFunc) ")"
  nth = "n" "+" (digit+ | propsFunc)
  
  /* composite selector */
  compositeSelector = selector combinator selector (combinator selector)*
  
  /* whole selector */
  basicSelector = rootSelector | universalSelector | attributeSelector | htmlTagSelector | jsxTagSelector

  /* named selector */
  attributeSelector = "[" kebabCase "=" lowerCase "]"
  htmlTagSelector = lowerCase digit* lowerCase*
  jsxTagSelector = pascalCase
  
  /* atomic selector */
  rootSelector = "&"
  universalSelector = "*"
  
  /* whole combinator */
  combinator = adjacentSiblijngCombinator | generalSiblijngCombinator | childCombinator | columnCombinator
  
  /* atomic combinator */
  adjacentSiblijngCombinator = "+"
  generalSiblijngCombinator = "~"
  childCombinator = ">"
  columnCombinator = "||"
  
  /* -------------------------------------------------------------------------- */
  /* util                                                                       */
  /* -------------------------------------------------------------------------- */
  
  /* math */
  formula = 
    | atomicFormula -- simple
    | formulaElements (mathOperator formulaElements)+ -- recursive
  formulaElements = 
    | numeral -- number
    | "(" atomicFormula ")" -- expression
  atomicFormula = numeral (mathOperator numeral)+
  numeral = numeralOnly | numeralWithUnit
  numeralOnly = 
    | "-" digit+ -- negative
    | digit+ -- positive
    | propsFunc -- injected
  numeralWithUnit = numeral unit
  unit = lowerCase
  mathOperator = "-" | "+" | "*" | "**" | "/"
  
  /* string */
  lowerCase = lower+
  kebabCase = lower+ ("-" lower+)*
  pascalCase = (upper (lower | digit)+)+
  jsIdentifier = letter (letter | digit | "_")*
  
  /* special character */
  whitespace = "\t"
             | "\x0B"    -- verticalTab
             | "\x0C"    -- formFeed
             | " "
             | "\u00A0"  -- noBreakSpace
             | "\uFEFF"  -- byteOrderMark
             | unicodeSpaceSeparator

  lineTerminator = "\n" | "\r" | "\u2028" | "\u2029"
  lineTerminatorSequence = "\n" | "\r" ~"\n" | "\u2028" | "\u2029" | "\r\n"
  
  unicodeSpaceSeparator = "\u2000".."\u200B" | "\u3000"
}