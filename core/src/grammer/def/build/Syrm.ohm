SkipToken {  
  Root = ""
  
  /* override */
  space := whitespace | lineTerminator | comment
  
  /* comment */
  comment = multiLineComment | singleLineComment

  multiLineComment = "/*" (~"*/" any)* "*/"
  singleLineComment = "//" (~lineTerminator any)*
  
  /* special character */
  whitespace = "\t"
             | "\x0B"    -- verticalTab
             | "\x0C"    -- formFeed
             | " "
             | "\u00A0"  -- noBreakSpace
             | "\uFEFF"  -- byteOrderMark
             | unicodeSpaceSeparator

  lineTerminator = "\n" | "\r" | "\u2028" | "\u2029"
  lineTerminatorSequence = "\n" | "\r" ~"\n" | "\u2028" | "\u2029" | "\r\n"
  
  unicodeSpaceSeparator = "\u2000".."\u200B" | "\u3000"
}

// nondivisible

Atomic <: SkipToken {
  /* props | number */
  generatedNumber = props | number
    
  /* props */
  props = "props" "(" jsIdentifier ")"
  
  /* paren */
  bparen = "("
  eparen = ")"
  
  /* numeral */
  number = 
    | "-" digit+ -- negative
    | digit+ -- positive
  
  /* unit */
  unit = lowerCase | "%"
  
  /* operator */
  operator = "-" | "+" | "*" | "**" | "/"
  
  /* string */
  string = lowerCase | kebabCase | pascalCase | camelCase
  lowerCase = lower+
  kebabCase = lower ("-" | lower)*
  pascalCase = (upper (lower | digit)*)+
  camelCase = lower (letter | digit)*
  
  /* jsIdentifier */
  jsIdentifier = letter (letter | digit | "_")*
}

// Combination of Atomic and Molecule

Primitive <: Atomic {
  Formula = 
    | WrapTerm operator Formula -- expression
    | WrapTerm
  WrapTerm =
    | bparen AtomicFormula eparen -- expression
    | numeral
  AtomicFormula = 
    | numeral operator AtomicFormula -- expression
    | numeral
  numeral = 
    | numeralWithUnit 
    | generatedNumber
  numeralWithUnit = generatedNumber unit
  
  generatedLiteral = props | literal
  literal = "\"" (~"\"" any)* "\""
}

SyrmedCssInterface <: Primitive {
  
  /* -------------------------------------------------------------------------- */
  /* WAI-ARIA                                                                   */
  /* -------------------------------------------------------------------------- */
    
  ariaLabel = kebabCase
  ariaValue = literal
  
  /* -------------------------------------------------------------------------- */
  /* selector list                                                              */
  /* -------------------------------------------------------------------------- */
  
  SelectorList = Selector ("," Selector)*
  
  /* -------------------------------------------------------------------------- */
  /* selector                                                                   */
  /* -------------------------------------------------------------------------- */
  
  /* Selector */
  Selector = 
    | EnumSelector combinator Selector -- composite
    | EnumSelector
  EnumSelector = 
    | basicSelector* PredicateSelector -- predicate
    | basicSelector+
  PredicateSelector = PseudoSelector | attributeSelector
  basicSelector = tagSelector | constantSelector
  
  /* tagSelector */
  tagSelector = htmlTagSelector | jsxTagSelector
  htmlTagSelector = lower (digit | lower)*
  jsxTagSelector = pascalCase
  
  /* atomic Selector */
  constantSelector = rootSelector | universalSelector
  rootSelector = "&"
  universalSelector = "*"
  
  /* -------------------------------------------------------------------------- */
  /* attribute                                                                  */
  /* -------------------------------------------------------------------------- */
  
  /* attributeSelector */
  attributeSelector = attributePredicate+
  
  attributePredicate = 
    | "[" kebabCase equal generatedLiteral "]" -- value
    | "[" kebabCase "]" -- has
    
  equal = "=" | "*=" | "$=" | "~="
  
  /* -------------------------------------------------------------------------- */
  /* pseudo                                                                     */
  /* -------------------------------------------------------------------------- */
  
  /* Pseudo Selector */
  PseudoSelector = Pseudo+
  
  /* Pseudo */
  Pseudo = 
    | "::" kebabCase ("(" PseudoArg ")")* -- element
    | ":" kebabCase ("(" PseudoArg ")")* -- class
  PseudoArg = nth | props | Selector | letter+ | digit+ 
  
  /* nth */
  nth = (nthTerm | generatedNumber | operator)+
  nthTerm = number* "n"
  
  /* -------------------------------------------------------------------------- */
  /* combinator                                                                 */
  /* -------------------------------------------------------------------------- */
  combinator = adjacentSiblijngCombinator | generalSiblijngCombinator | childCombinator | columnCombinator
  adjacentSiblijngCombinator = "+"
  generalSiblijngCombinator = "~"
  childCombinator = ">"
  columnCombinator = "||"
  
  /* -------------------------------------------------------------------------- */
  /* PropertyName                                                               */
  /* -------------------------------------------------------------------------- */
  
  PropertyName = kebabCase | collectionKeyword
  collectionKeyword = "@" "collection"
  
  /* -------------------------------------------------------------------------- */
  /* PropertyValue                                                              */
  /* -------------------------------------------------------------------------- */
  
  PropertyValue = PropertyValueFunc | Formula | kebabCase | pascalCase
  PropertyValueFunc = kebabCase "(" Formula ("," Formula)* ")"
}

DeclarationBlock <: SyrmedCssInterface {
  
  /* -------------------------------------------------------------------------- */
  /* WAI-ARIA                                                                   */
  /* -------------------------------------------------------------------------- */
  
  AriaDeclarationBlock = "{" AriaDeclaration+ "}"
  AriaDeclaration = ariaLabel ":" ariaValue ";"
  
  /* -------------------------------------------------------------------------- */
  /* CSS                                                                        */
  /* -------------------------------------------------------------------------- */
  
  DeclarationBlock = "{" Declaration+ "}"
  Declaration = PropertyName ":" PropertyValue+ ";"
}

RuleSet <: DeclarationBlock {
  
  /* -------------------------------------------------------------------------- */
  /* WAI-ARIA                                                                   */
  /* -------------------------------------------------------------------------- */
  
  AriaRuleSet = SelectorList AriaDeclarationBlock
  
  /* -------------------------------------------------------------------------- */
  /* CSS                                                                        */
  /* -------------------------------------------------------------------------- */
  
  RuleSet = SelectorList DeclarationBlock
}

RuleSetStatement <: RuleSet {

  /* -------------------------------------------------------------------------- */
  /* WAI-ARIA                                                                   */
  /* -------------------------------------------------------------------------- */
  
  AriaRuleSetStatement =
    | ifAnnotation AriaRuleSet AriaRuleSet invert -- invert
    | ifAnnotation AriaRuleSet+ blockend -- if_block
    | ifAnnotation AriaRuleSet else AriaRuleSet -- if_else
    | ifAnnotation AriaRuleSet -- if
    | AriaRuleSet
  
  /* -------------------------------------------------------------------------- */
  /* CSS                                                                        */
  /* -------------------------------------------------------------------------- */
  
  RuleSetStatement = 
    | ifAnnotation RuleSet RuleSet invert -- invert
    | ifAnnotation RuleSet+ blockend -- if_block
    | ifAnnotation RuleSet else RuleSet -- if_else
    | ifAnnotation RuleSet -- if
    | RuleSet
  
  /* -------------------------------------------------------------------------- */
  /* annotation                                                                 */
  /* -------------------------------------------------------------------------- */
  ifAnnotation = truthy | falsy | exist
    
  truthy = "@" "truthy" "(" jsIdentifier ")"
  falsy = "@" "falsy" "(" jsIdentifier")"
  exist = "@" "exist" "(" jsIdentifier ")"
  
  else = "@" "else"
  
  invert = "@" "invert"
  blockend = "@" "end"
}

Namespace <: RuleSetStatement {
  Namespace<TagName> = "<" TagName ">" space* RuleSetStatement* space* "</" TagName ">"
}

Syrm <: Namespace {
  Root := Block*
  
  Block = WaiariaBlock | CascadeBlock | CollectionBlock | Block
  
  WaiariaBlock = "<WAIARIA>" space* AriaRuleSetStatement* space* "</WAIARIA>"
  CascadeBlock = "<Cascade>" space* RuleSetStatement* space* "</Cascade>"
  CollectionBlock = "<Collection>" space* Namespace<pascalCase>* space* "</Collection>"
}