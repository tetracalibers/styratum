SkipToken {  
  Root = ""
  
  /* override */
  space := whitespace | lineTerminator | comment
  
  /* comment */
  comment = multiLineComment | singleLineComment

  multiLineComment = "/*" (~"*/" any)* "*/"
  singleLineComment = "//" (~lineTerminator any)*
  
  /* special character */
  whitespace = "\t"
             | "\x0B"    -- verticalTab
             | "\x0C"    -- formFeed
             | " "
             | "\u00A0"  -- noBreakSpace
             | "\uFEFF"  -- byteOrderMark
             | unicodeSpaceSeparator

  lineTerminator = "\n" | "\r" | "\u2028" | "\u2029"
  lineTerminatorSequence = "\n" | "\r" ~"\n" | "\u2028" | "\u2029" | "\r\n"
  
  unicodeSpaceSeparator = "\u2000".."\u200B" | "\u3000"
}

// nondivisible

Atomic <: SkipToken {
  atomic = addedAtomic | pureAtomic
  
  pureAtomic = number | unit | operator | string | jsIdentifier
  addedAtomic = props
  
  /* props | number */
  generatedNumber = props | number
    
  /* props */
  props = "props" "(" jsIdentifier ")"
  
  /* numeral */
  number = 
    | "-" digit+ -- negative
    | digit+ -- positive
  
  /* unit */
  unit = lowerCase | "%"
  
  /* operator */
  operator = "-" | "+" | "*" | "**" | "/"
  
  /* string */
  string = lowerCase | kebabCase | pascalCase | camelCase
  lowerCase = lower+
  kebabCase = lower ("-" | lower)*
  pascalCase = (upper (lower | digit)*)+
  camelCase = lower (letter | digit)*
  
  /* jsIdentifier */
  jsIdentifier = letter (letter | digit | "_")*
}

// Combination of Atomic and Molecule

Primitive <: Atomic {
  Formula = FormulaElements (operator FormulaElements)*
  FormulaElements = 
    | numeral -- number
    | "(" AtomicFormula ")" -- expression
  AtomicFormula = numeral (operator numeral)+
  numeral = numeralWithUnit | generatedNumber
  numeralWithUnit = generatedNumber unit
}

SyrmedCssInterface <: Primitive {
  pureAtomic += collectionKeyword | combinator | constantSelector
  addedAtomic += tagSelector
  
  /* -------------------------------------------------------------------------- */
  /* selector list                                                              */
  /* -------------------------------------------------------------------------- */
  
  SelectorList = Selector ("," Selector)*
  
  /* -------------------------------------------------------------------------- */
  /* selector                                                                   */
  /* -------------------------------------------------------------------------- */
  
  /* Selector */
  Selector =
    | AtomicSelector RelationalSelector+ -- composite
    | AtomicSelector
  RelationalSelector = 
    | combinator ~"&" AtomicSelector -- specified
    | ~"&" AtomicSelector -- implicated
  AtomicSelector = 
    | basicSelector PseudoSelector -- composite
    | basicSelector
    | PseudoSelector
  basicSelector = attributeSelector | htmlTagSelector | constantSelector
  
  /* attributeSelector */
  attributeSelector = "[" kebabCase "=" lowerCase "]"
  
  /* tagSelector */
  tagSelector = htmlTagSelector | jsxTagSelector
  htmlTagSelector = lower (digit | lower)*
  jsxTagSelector = pascalCase
  
  /* atomic Selector */
  constantSelector = rootSelector | universalSelector
  rootSelector = "&"
  universalSelector = "*"
  
  /* -------------------------------------------------------------------------- */
  /* pseudo                                                                     */
  /* -------------------------------------------------------------------------- */
  
  /* Pseudo Selector */
  PseudoSelector = Pseudo+
  
  /* Pseudo */
  Pseudo = 
    | "::" kebabCase ("(" PseudoArg ")")* -- element
    | ":" kebabCase ("(" PseudoArg ")")* -- class
  PseudoArg = nth | props | Selector | letter+ | digit+ 
  
  /* pseudo helper */
  nth = "n" "+" generatedNumber
  
  /* -------------------------------------------------------------------------- */
  /* combinator                                                                 */
  /* -------------------------------------------------------------------------- */
  combinator = adjacentSiblijngCombinator | generalSiblijngCombinator | childCombinator | columnCombinator
  adjacentSiblijngCombinator = "+"
  generalSiblijngCombinator = "~"
  childCombinator = ">"
  columnCombinator = "||"
  
  /* -------------------------------------------------------------------------- */
  /* PropertyName                                                               */
  /* -------------------------------------------------------------------------- */
  
  PropertyName = kebabCase | collectionKeyword
  collectionKeyword = "@" "collection"
  
  /* -------------------------------------------------------------------------- */
  /* PropertyValue                                                              */
  /* -------------------------------------------------------------------------- */
  
  PropertyValue = PropertyValueFunc | Formula | kebabCase | pascalCase
  PropertyValueFunc = kebabCase "(" Formula ("," Formula)* ")"
}

DeclarationBlock <: SyrmedCssInterface {
  DeclarationBlock = "{" Declaration+ "}"
  
  Declaration = PropertyName ":" PropertyValue+ ";"
}

RuleSet <: DeclarationBlock {
  RuleSet = SelectorList DeclarationBlock
}

RuleSetStatement <: RuleSet {
  pureAtomic += else | invert
  addedAtomic += truthy | falsy | exist
  
  /* -------------------------------------------------------------------------- */
  /* ruleset statement                                                          */
  /* -------------------------------------------------------------------------- */
  
  RuleSetStatement = 
    | ifAnnotation RuleSet RuleSet invert -- invert
    | ifAnnotation RuleSet else RuleSet -- if_else
    | ifAnnotation RuleSet -- if
    | RuleSet
  
  /* -------------------------------------------------------------------------- */
  /* annotation                                                                 */
  /* -------------------------------------------------------------------------- */
  ifAnnotation = truthy | falsy | exist
    
  truthy = "@" "truthy" "(" jsIdentifier ")"
  falsy = "@" "falsy" "(" jsIdentifier")"
  exist = "@" "exist" "(" jsIdentifier ")"
  else = "@" "else"
  invert = "@" "invert"
}

Namespace <: RuleSetStatement {
  Namespace<TagName> = "<" TagName ">" space* RuleSetStatement* space* "</" TagName ">"
}

Syrm <: Namespace {
  Root := Block*
  
  Block = CascadeBlock | CollectionBlock | Block
  
  CascadeBlock = "<Cascade>" space* RuleSetStatement* space* "</Cascade>"
  CollectionBlock = "<Collection>" space* Namespace<pascalCase>* space* "</Collection>"
}